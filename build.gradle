// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        minSdkVersion = 23
        sdkVersion = 29

        kotlinVersion = '1.5.31'

        coroutinesVersion = '1.5.2'
        navigationVersion = '2.3.5'
        lifecycleVersion = '2.2.0'
        koinVersion = '2.2.3'
        retrofitVersion = '2.9.0'
        appcompatVersion = '1.3.1'
        coreVersion = '1.6.0'
        constraintLayoutVersion = '2.1.1'
        materialVersion = '1.4.0'
        insetterVersion = '0.3.1'
        moshiVersion = "1.12.0"

        firebaseVersion = '17.4.1'
        crashlyticsVersion = '17.0.0'

        beagleVersion = '2.6.4'
        beagleUiType = 'ui-drawer'
        beagleNetworkLoggerType = "okhttp"

        testRunnerVersion = '1.4.0'
        espressoVersion = '3.4.0'
        junitVersion = '4.13.2'

        detektVersion = '1.9.1'

        lintVersion = '30.0.3'
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            url "https://maven.pkg.github.com/halcyonmobile/halcyon-custom-gradle-publish-plugin"
            credentials {
                username = project.findProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.1.1'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath 'com.halcyonmobile.publish.custom:java-and-aar:2.1.0'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            url "https://maven.pkg.github.com/halcyonmobile/halcyon-custom-gradle-publish-plugin"
            credentials {
                username = project.findProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    toolVersion = "$detektVersion"

    autoCorrect = true
    failFast = false

    buildUponDefaultConfig = true

    input = files("$projectDir/app/src/main/java", "$projectDir/core/src/main/java", "$projectDir/error-handler-core/src/main/java", "$projectDir/error-handler-rest/src/main/java")

    config = files("default-detekt-config.yml")

    reports {
        html {
            enabled = true
            destination = file("build/reports/detekt.html")
        }
        xml.enabled = false
        txt.enabled = false
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    jvmTarget = "1.8"
    exclude(".*/resources/.*,.*/build/.*")
}

apply plugin: 'io.gitlab.arturbosch.detekt'
apply from: "./deploy.gradle"